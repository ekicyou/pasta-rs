// SHIORI 3.0, 2.0 parser

req         = ${ SOI ~ header ~ key_values ~ _eol ~ EOI }
key_values  = ${ key_value* }

key_value   = ${ key ~ tag ~ value ~ _eol }
value       = @{ remain }
key         = @{ id }

header      = ${ method ~ _sp ~ ( header3 | header2 ) }

header2     = ${ id ~ _sp ~ _shiori2 ~ ver ~ _eol }

header3     = ${ _shiori3 ~ _eol }
method      = ${ get | notify }

id          = @{ XID_START ~ XID_CONTINUE* }

// tokens
_shiori2    = _{ "SHIORI/2." }
_shiori3    = _{ "SHIORI/3.0" }
ver         = @{ ASCII_DIGIT }
get         =  { "GET" }
notify      =  { "NOTIFY" }
tag         = _{ ": " }
remain      = @{ ( !eol ~ ANY )* }

// chars
_sp     = _{ " " }
_eol    = _{ "\r\n" }
sp      =  { " " }
eol     =  { "\r\n" }
key_sep = { "-" | "." }

