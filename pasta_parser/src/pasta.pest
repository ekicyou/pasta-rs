/// # パスタスクリプト　字句構造
///
/// ## 1 エスケープ文字
/// ### 1-1 「＠」を識別文字として使える文脈で、
/// 「＠」の直後の文字が空白以外、かつキーワード文字以外であるとき、
/// その文字を文字としてそのまま扱う。
/// > 例）＠＠　→「＠」
esc_char    = ${ esc1 ~ esc2 }
esc1        = _{ AT }
esc2        = @{ !( XID_CONTINUE | SP | EOL ) ~ ANY }


/// ## 2 行の定義とコメント、空行、文書コメント
/// ### 2-1 「＃」文字から行末までをコメントとする。
comment         = ${ comment_mark ~ comment_str }
comment_mark    = _{ SHARP ~ skip_sp }
comment_str     = @{ skip_eol }

/// ### 2-2 残りが空白のみで構成される行を空行とする。
spaces_line     = @{ skip_sp ~ comment? ~ EOL }

/// ### 2-3 最初の柱行までは文頭コメントとして無視する。
doc_comment         = @{ doc_comment_line* }
doc_comment_line    = @{ !hasira_mark ~ skip_line } 

/// ### 2.4 識別エラーの判定
/// 行末は、コメント・識別エラーのいずれかで始まり、
/// EOLで終わる。
eol_check   = @{ skip_sp ~ ( comment | error ) ~ EOL }
error       = @{ error_token ~ skip_eol }
error_token = @{ NOT_EOL }

/// ### 2.5 行定義
/// 行定義は「[柱/アクター]行」・「[ト書き/セリフ]行」・「空行」のいずれかで認識され、
/// 未識別文字列について行末まで確認する。
line = @{ ( hasira ) ~ eol_check }


/// ## 3 [柱/アクター]行（セクション）
/// ### 3.1
/// ０以上の「・」に続けて有効な文字列で始まる柱行（セクション開始）とする。
/// 柱行は空白またはEOLで始まってはならない。

hasira_mark     = _{ DOT }
hasira_start    = _{ !( SP | EOL | comment_mark ) ~ hasira_mark* }

/// ### 3.2
/// 柱はマーク・タイトル・属性の列挙で構成される。
hasira      = @{ hasira_start ~ h_title? ~ h_attrs? }
h_title     = @{ (!end_title ~ ANY)+ }
end_title   = _{ (COLON | SP | comment_mark) ~ skip_sp }
h_attrs     = @{ end_title ~ h_attr* }
h_attr      = @{ ( require | either | forget | memory | action ) ~ skip_sp }


// ## 4 [ト書き/セリフ]行
/// ### 4.1
/// ト書き/セリフ行はスペースで始まり、有効要素が１つ以上並ぶ。
togaki      = @{t_start ~ t_item+ }
t_start     = _{ spaces }
t_item      = @{ serif | t_attr }
t_attr      = @{ ( action ) ~ skip_sp }

// ## 5 セリフ
/// ### 5.1
/// セリフはコメント・アクション・EOLと認識されないすべての文字の
/// １つ以上の列挙で構成される。
/// ### 5.2
/// 「＠」「＃」を使いたい場合は、それぞれ「＠＠」「＠＃」を使う。
serif       = @{ (s_normal | s_escape )+ }
s_normal    = @{ !( AT | SHARP | EOL ) ~ ANY }
s_escape    = ${ AT ~ s_esc_char }
s_esc_char  = @{ ( AT | SHARP ) } 


/// ## 6 属性
/// ### 6.1
/// 属性式
action      = @{ AT     ~ expr }    /// ＠アクション
require     = @{ EXC    ~ expr }    /// ！すべてに一致
either      = @{ QUE    ~ expr }    /// ？いずれかに一致
forget      = @{ MINUS  ~ expr }    /// ー忘れる
memory      = @{ PLUS   ~ expr }    /// ＋覚える
expr        = @{ id }



// token

id          = @{ XID_START ~ XID_CONTINUE* }

spaces      = @{ SP+ }
skip_sp     = _{ SP* }
skip_eol    = _{ NOT_EOL* }
skip_line   = _{ skip_eol ~ EOL }

// MARK, CHAR

SP      = _{ SPACE_SEPARATOR | TAB }
TAB     = _{ "\t" }
DOT     = _{ "・" | "･" }
AT      = _{ "＠" | "@" }
EXC     = _{"！" | "!" }

QUE     = _{ "？" | "?" }
PLUS    = _{ "＋" | "+" }
MINUS   = _{ "－" | "-" }

COLON   = _{ "：" | ":" }
CONMA   = _{ "、" |"，" |"､" |"," }
SHARP   = _{ "＃" | "♯" | "#" }

EOL     = _{ "\r\n" | "\n" | "\r" | EOI }
NOT_EOL = _{ !EOL ~ ANY }

// EOF

